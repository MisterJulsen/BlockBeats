plugins {
    id 'dev.architectury.loom' version '1.7-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = "$rootProject.minecraft_version${rootProject.release_channel != "release" ? "-$rootProject.release_channel" : ""}-$rootProject.mod_version"
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        maven { url = "file:///${System.getProperty('user.home')}/.m2/github/modsrepo/maven/" } // DragonLib
        maven { url = "https://raw.githubusercontent.com/MisterJulsen/modsrepo/main/maven" } // DragonLib
        maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // Forge Config API
        maven { url = "https://maven.parchmentmc.org" } // ParchmentMC
        maven { url = "https://mvnrepository.com/artifact/ws.schild/jave-all-deps"} // jave
        maven { url = "https://maven.terraformersmc.com/" } // ModMenu
        maven { url = "https://maven.nucleoid.xyz/" } // Nucleoid
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_version}@zip")
        }
    }

    processResources {
        def expandProps = [
            "version": version,
            "minecraft_version": rootProject.minecraft_version,
            "issues": rootProject.issues,
            "source": rootProject.source,
            "license": rootProject.license,
            "modid": rootProject.archives_name,
            "display_name": rootProject.display_name,
            "homepage": rootProject.homepage,
            "authors": rootProject.authors,
            "description": rootProject.description,
            "icon": rootProject.icon,
            "discord": rootProject.discord,
            "next_unsupported_minecraft_version": rootProject.next_unsupported_minecraft_version,
            "dragonlib_version": "$rootProject.minecraft_version-$rootProject.dragonlib_version",
            "dragnsounds_version": rootProject.dragnsounds_version,
            "neoforge_loader_version": rootProject.neoforge_loader_version,
            "neoforge_dependency_version": rootProject.neoforge_dependency_version,
            "fabric_loader_version": rootProject.fabric_loader_version,
            "java_version": rootProject.java_version
        ]
        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/neoforge.mods.toml']) {
            expand expandProps
        }
        inputs.properties(expandProps)
        exclude ".cache" // Remove cache from generated data
    }

    java {
        withSourcesJar()
        withJavadocJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    // Configure Maven publishing.
    publishing {
    }
}
