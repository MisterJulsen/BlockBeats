plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

tasks {
    processResources {
        from(project(":common").file("src/main/resources/blockbeats.accesswidener")) {
            into("/assets/")
        }
    }

    remapJar {
        dependsOn(processResources)
        atAccessWideners.add("blockbeats.accesswidener")
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modCompileOnly("dev.architectury:architectury-neoforge:${rootProject.architectury_api_version}")
    modImplementation("de.mrjulsen.mcdragonlib:dragonlib-neoforge:${rootProject.minecraft_version}-${rootProject.dragonlib_version}")
    modImplementation("de.mrjulsen.dragnsounds:dragnsounds-neoforge:${rootProject.minecraft_version}-${rootProject.dragnsounds_version}")
    
    modImplementation(forgeRuntimeLibrary("ws.schild:jave-core:${rootProject.jave_version}"))
    modImplementation(forgeRuntimeLibrary("ws.schild:jave-nativebin-linux32:${rootProject.jave_version}"))
    modImplementation(forgeRuntimeLibrary("ws.schild:jave-nativebin-linux64:${rootProject.jave_version}"))
    modImplementation(forgeRuntimeLibrary("ws.schild:jave-nativebin-linux-arm32:${rootProject.jave_version}"))
    modImplementation(forgeRuntimeLibrary("ws.schild:jave-nativebin-linux-arm64:${rootProject.jave_version}"))
    modImplementation(forgeRuntimeLibrary("ws.schild:jave-nativebin-win32:${rootProject.jave_version}"))
    modImplementation(forgeRuntimeLibrary("ws.schild:jave-nativebin-win64:${rootProject.jave_version}"))
    modImplementation(forgeRuntimeLibrary("ws.schild:jave-nativebin-osxm1:${rootProject.jave_version}"))
    modImplementation(forgeRuntimeLibrary("ws.schild:jave-nativebin-osx64:${rootProject.jave_version}"))

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
